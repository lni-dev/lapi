
plugins {
    id 'java'
    id 'maven-publish'
    id 'java-library'
    id 'signing'
}

group = 'io.github.lni-dev'
archivesBaseName = 'lapi'
version = '1.0.5'

targetCompatibility = JavaVersion.VERSION_11
sourceCompatibility = JavaVersion.VERSION_11
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

tasks.named('compileJava') {
    options.javaModuleVersion = provider { project.version }
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': 'LApi',
                'Implementation-Version': project.version,
                'Automatic-Module-Name': 'me.linusdev.lapi')
    }
}

dependencies {
    api 'io.github.lni-dev:data:2.0.9'
    api 'org.jetbrains:annotations:23.0.0'
}

tasks.withType(JavaExec) {
    enableAssertions = true
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.encoding = 'UTF-8'
    options.addStringOption('Xdoclint:none', '-quiet')
}

publishing {
    repositories {
        maven {
            if(project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")){
                name = "OSSRH"
                def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }

            }else {
                project.logger.lifecycle("no OSSRH username and password given. Cannot deploy.")
            }
        }

        maven {
            if(project.hasProperty("githubUsername") && project.hasProperty("githubToken")){
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/lni-dev/lapi")
                credentials {
                    username = githubUsername
                    password = githubToken
                }
            }else {
                project.logger.lifecycle("no github username and token given. Cannot publish package.")
            }
        }
    }

    publications {

        mavenJava(MavenPublication) {
            artifactId = 'lapi'
            from components.java
            pom {
                name = 'LApi'
                description = 'A Discord Bot API written in Java'
                url = 'https://lni-dev.github.io/'
                groupId = 'io.github.lni-dev'
                version = project.version

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'linusdev'
                        name = 'Linus Andera'
                        email = 'einsuperlinus@gmail.com'
                        organizationUrl = 'https://lni-dev.github.io/'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/lni-dev/lapi.git'
                    developerConnection ='scm:git:ssh://github.com:lni-dev/lapi.git'
                    url = 'http://github.com/lni-dev/lapi/tree/master'
                }
            }
        }
    }
}

signing {
    if(project.hasProperty("signing.keyId") && project.hasProperty("signing.password") && project.hasProperty("signing.secretKeyRingFile")){
        sign publishing.publications.mavenJava
    }else {
        project.logger.lifecycle("No Signing information given. Cannot sign.")
    }
}

